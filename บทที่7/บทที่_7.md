# บทที่ 7 GPS, Google map, Firebase

* แนะนำ GPS มีเนื้อหาข้อมูลโดยทั่วไปของ GPS
* แนะนำ Google map มีเนื้อหาข้อมูลทั่วไปของ Google map
* แนะนำ Firebase มีเนื้อหาข้อมูลโดยทั่วไปของ Firebase และการใช้งาน Firebase เบื้องต้น

No. |ชื่อเรื่อง|
----- |----- |
1)|[Input Digital, Pull up, Pull down](https://github.com/phisic1714/IoT-Learning-Set/blob/Pea/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%882/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%88_2.md#input-digital--pull-up-pull-down)|
2)|[Debounce](https://github.com/phisic1714/IoT-Learning-Set/blob/Pea/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%882/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%88_2.md#debounce)|
3)|[Interrupt](https://github.com/phisic1714/IoT-Learning-Set/blob/Pea/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%882/%E0%B8%9A%E0%B8%97%E0%B8%97%E0%B8%B5%E0%B9%88_2.md#interrupt)|

---
### GPS 
* ย่อมาจาก Global Positioning System ซึ่งเป็นระบบนำทางที่ใช้สัญญาณจากดาวเทียมเพื่อกำหนดตำแหน่งของอุปกรณ์ที่ต้องการใช้งาน ระบบ GPS จะใช้สัญญาณจากดาวเทียมหลายตัวเพื่อหาตำแหน่งที่แน่นอนของอุปกรณ์ที่ต้องการติดตาม และสามารถนำข้อมูลพิกัดตำแหน่งนั้นไปใช้งานในการนำทางหรือติดตามตำแหน่งได้ ระบบ GPS มีการใช้งานแพร่หลายในต่าง ๆ เช่น ในการนำทางรถยนต์ การเดินทางท่องเที่ยว การสำรวจและติดตามการเคลื่อนไหวของพาหนะ และการนำทางอื่น ๆ ที่ต้องใช้งานที่มีความแม่นยำในการกำหนดตำแหน่งเป็นสิ่งสำคัญ

### SIM808

* เป็นอุปกรณ์ที่สามารถเชื่อมต่อกับเครือข่าย GSM/ GRPRS, การรับส่งข้อมูลแบบ GPRS, การทำงานแบบเสียงและ SMS, ระบบตำแหน่งพิกัดด้วย GPS ทำให้มีสามารถในการติดตามตำแหน่งได้ นอกจากนี้ยังมีความสามารถในการเชื่อมต่อ Bluetooth ด้วย โดยขาของ SIM808 มีดังนี้

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2020.42.08.png)



-	Bluetooth Antenna ช่องนี้ใช้สำหรับเสียบเพื่อให้ SIM808 สามารถเชื่อมต่อกับอุปกรณ์ที่รองรับการสื่อสารผ่าน Bluetooth 
-	GPS Antenna ช่องนี้สำหรับเสียบเพื่อให้มีความสามารถในการรับสัญญาณ GPS และสามารถระบุตำแหน่งพิกัดทางภูมิศาสตร์ได้
-	RX (Recive) และ TX (Transmit) สำหรับขา RX และ TX ใช้ในการรับและส่งข้อมูล เพื่อให้สามารถสื่อสารกับไมโครคอนโทรลเลอร์หรืออุปกรณ์ที่ต้องการเชื่อมต่อ
-	5V ขานี้ใช้สำหรับจ่ายพลังงาน
-	GND ขานี้สำหรับต่อกราวด์
-	Start Button สำหรับปุ่มนี้ใช้ในการเริ่มต้น หรือรีเซ็ตอุปกรณ์ SIM808 

จากที่กล่าวมา SIM808 มีความสามารถที่หลากหลาย เหมาะสำหรับใช้ในโปรเจกต์ที่ต้องการเชื่อมต่อกับเครือข่าย ทำให้เป็นอุปกรณ์ที่สามารถนำมาประยุกต์ใช้ในหลายงานต่าง ๆ

<details><summary>CLICK : <ins>ตัวอย่างที่ 
7.1 การแสดงค่าละติจูด ลองติจูดผ่าน Serial Monitor</ins></summary>**
<p>

```ruby
#include <DFRobot_sim808.h>
#include <SoftwareSerial.h>

#define PIN_RX D1
#define PIN_TX D2
SoftwareSerial mySerial(PIN_TX,PIN_RX);
DFRobot_SIM808 sim808(&mySerial);
const int READ_LOCATION =0 ;
int state ;
void setup() {
state = READ_LOCATION ;
mySerial.begin(9600);
Serial.begin(9600);
while (!sim808.attachGPS())
{
    Serial.println("Open the GPS power failure");
  }
              Serial.println("Open the GPS power success");
  }

void loop() {
switch(state){
case READ_LOCATION :
     if (sim808.getGPS()) {

      Serial.print("latitude :");
      Serial.println(sim808.GPSdata.lat);
      Serial.print("longitude :");
      Serial.println(sim808.GPSdata.lon);
      Serial.println();
 }
break;
}
}
```
<p>
</details>

* จากโค้ดตัวอย่าง 7.1 รับค่า ละติจูด ลองจิจูด จากโมดูล SIM808 และแสดงค่า ละติจูด ลองจิจูด ที่ Serial Monitor


![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2020.45.13.png)

* จากรูป State Diagram การแสดงค่าละติจูด ลองติจูดผ่าน Serial Monitor state เริ่มต้นคือREAD_LOCATION รับค่า ค่าละติจูด ลองติจูด จากโมดูล SIM808 และแสดงค่าบน serial ทำงานซ้ำไปเรื่อยๆ

### Firebase 

* เป็นแพลตฟอร์มบนคลาวด์ที่ถูกพัฒนาโดย Google มีเครื่องมือและบริการหลากหลายสำหรับนักพัฒนาในการสร้าง ทดสอบ ปรับใช้แอปพลิเคชันมือถือและเว็บไซต์ Firebase นำเสนอฐานข้อมูลแบบเรียลไทม์และสามารถบันทึกข้อมูลได้ เป็นตัวกลางเชื่อมต่ออุปกรณ์ได้หลายเครื่อง มีบริการสนับสนุนที่หลากหลาย ได้แก่ อีเมล, รหัสผ่านและการเข้าสู่ระบบที่เก็บข้อมูลบนคลาวด์

#### บริการอื่นๆ ที่สามารถใช้งานใน Firebase

*	Realtime Database: ฐานข้อมูลแบบ NoSQL (Non-relational database)
สามารถซิงโครไนซ์ข้อมูลในเวลาจริงได้ โดยอัปเดตข้อมูลทันทีเมื่อมีการเปลี่ยนแปลงใดๆ เกิดขึ้น ซึ่งทำให้สามารถเข้าถึงข้อมูลล่าสุดได้ตลอดเวลา และลดความซ้ำซ้อนในการอัปเดตข้อมูลเอง การจัดเก็บข้อมูลอยู่ในรูป JSON

*	Firestore : เป็นฐานข้อมูลแบบ NoSQL ที่มีความยืดหยุ่นสูงและมีประสิทธิภาพสำหรับการจัดเก็บข้อมูล รองรับการเรียกข้อมูลแบบคิว (Query) และการจัดการโครงสร้างข้อมูลที่ซับซ้อน เหมาะสำหรับแอปพลิเคชันที่ต้องการการค้นหาและกรองข้อมูล

*	Authentication: ช่วยในการจัดการระบบการรับรองตัวตนของผู้ใช้ เช่น การลงทะเบียน การเข้าสู่ระบบ และการจัดการสิทธิ์การเข้าถึงแอปพลิเคชัน

*	Storage: บริการจัดเก็บและจัดการไฟล์หรือข้อมูลที่เกี่ยวข้องกับแอปพลิเคชัน เช่น รูปภาพ วิดีโอ และเอกสาร

*	Hosting: บริการเว็บโฮสติ้งที่ช่วยให้เผยแพร่และจัดการเว็บไซต์หรือแอปพลิเคชันเว็บได้อย่างง่าย

*	Cloud Functions: ช่วยในการเขียนและดำเนินการรหัสที่ทำงานบนเซิร์ฟเวอร์เมื่อมีการทำงานในแอปพลิเคชัน เช่น เมื่อมีการเพิ่มข้อมูลลงในฐานข้อมูล

*	Cloud Messaging: ช่วยในการส่งการแจ้งเตือน (Push Notifications) ไปยังผู้ใช้งานในแอปพลิเคชัน

*	Performance Monitoring: ช่วยในการตรวจสอบและวิเคราะห์ประสิทธิภาพของแอปพลิเคชัน เช่น เวลาการโหลดและประสิทธิภาพการทำงาน

*	Remote Config: ช่วยในการปรับแต่งแอปพลิเคชันได้จากระยะไกลโดยไม่ต้องอัพเดตแอปพลิเคชันโดยตรง

*	ML Kit: ชุดเครื่องมือเรียนรู้เชิงลึกสำหรับการวิเคราะห์และประมวลผลข้อมูล เช่น การตรวจจับวัตถุในภาพ การแปลภาษา และการจำแนกสิ่งต่าง ๆ
 
### JSON

ย่อมาจาก JavaScript Object Notaion เป็นข้อมูลรูปแบบ text ที่มีการเก็บข้อมูลแบบ key,value 

ตัวอย่างการเขียน '{"key": "value"}'

#### ประเภทข้อมูลที่ JSON สามารถเก็บได้
*	String : กลุ่มตัวอักษร
*	Number : ตัวเลข
*	Array : ชุดข้อมูล
*	boolean : True or False
*	object (JSON object) : ชุดข้อมูลที่เป็น Key-Value แบบ strings โดยใช้สัญลักษณ์ปีกกา ตัวอย่าง {key1:value1, key2:value2 }
*	null : ค่าว่าง

### node-red-contrib-web-worldmap

เป็นส่วนขยายที่เพิ่มความสามารถในการแสดงแผนที่เว็บใน Node-RED โดยใช้ข้อมูลที่ตำแหน่งภูมิศาสตร์ เช่น พิกัดละติจูดและลองจิจูด หรือข้อมูลอื่น ๆ ที่สามารถนำมาแสดงบนแผนที่ Node-RED ได้

#### key ต่างๆ สำหรับควบคุม Node red worldmap

*	layer: ระบุชั้นบนแผนที่เพื่อเพิ่มเครื่องหมาย.
*	heading: ใช้ร่วมกับความเร็วเพื่อวาดเวกเตอร์.
*	speed: ใช้ร่วมกับหัวเพื่อวาดเวกเตอร์.
*	icon: ชื่อไอคอน 
*	iconColor: ชื่อสีมาตรฐานของ CSS หรือค่า hex #rrggbb.
*	SIDC: รหัสสัญลักษณ์ NATO (แทนไอคอน).
*	label: ป้ายชื่อติดกับเครื่องหมาย 
*	ttl: เวลาในการใช้งานของเครื่องหมายแต่ละรายการก่อนลบ
*	photoUrl: เพิ่มรูปภาพที่ชี้ไปยัง URL ในกล่องป็อปอัพ.
*	videoUrl: เพิ่มไฟล์ mp4 ที่ชี้ไปยัง URL ในกล่องป๊อปอัพ.
*	weblink: ลิงก์ไปยังเว็บเพจภายนอก
 
<details><summary>CLICK : <ins>ตัวอย่างที่ 
7.2 การรับค่าลองติจูด ละติจูด เก็บข้อมูลที่ Firebase</ins></summary>**
<p>

```ruby
#include <Arduino.h>

#include <Firebase_ESP_Client.h> // Firebase library
#include <ESP8266WiFi.h>
#include "addons/TokenHelper.h" // Firebase token generation
#include "addons/RTDBHelper.h"  // Firebase Realtime Database helper
#include <DFRobot_sim808.h>
#include <SoftwareSerial.h>
#define WIFI_SSID "xxxx"
#define WIFI_PASSWORD "xxxx"
#define API_KEY "xxxx"      // API key ของ Firebase ที่ได้รับจากการสร้างโปรเจกต์ใน Firebase Console
#define USER_EMAIL "xxxx@xxxx.xxx"     // อีเมลของบัญชี Firebase ที่ใช้สำหรับเข้าสู่ระบบ
#define USER_PASSWORD "xxxx"    // รหัสผ่านของบัญชี Firebase ที่ใช้สำหรับเข้าสู่ระบบ
#define DATABASE_URL "https://xxxx.xxx" // URL ของ Firebase Realtime Database
#define PIN_RX D1
#define PIN_TX D2
SoftwareSerial mySerial(PIN_TX, PIN_RX);
DFRobot_SIM808 sim808(&mySerial);
void readLatLon();              // ฟังก์ชันสำหรับอ่านค่าอุณหภูมิและความชื้นจากเซ็นเซอร์
void sendDataToFirebase(String path, String value); // ฟังก์ชันสำหรับส่งค่าอุณหภูมิและความชื้นไปยัง Firebase
FirebaseData fbdo;              // Object สำหรับเก็บข้อมูลที่ได้จาก Firebase
FirebaseAuth auth;               // Object สำหรับเก็บข้อมูลการเข้าสู่ระบบ
FirebaseConfig config;           // Object สำหรับเก็บข้อมูลการเชื่อมต่อ Firebase
String uid;                          // ตัวแปรสำหรับเก็บค่า User ID
String databasePath;           // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database
String latPath;                  // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database สำหรับอุณหภูมิ
String lonPath;                        // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database สำหรับความชื้น
String namePath;
String longtitute;
String latitute;
const int READ_LOCATION = 0;
const int SEND_DATA = 1;
int state;
void initWiFi()
{
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED)
  {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
  Serial.println();
}
void setup()
{
  state = READ_LOCATION;
  mySerial.begin(9600);
  Serial.begin(115200);
  initWiFi();
  config.api_key = API_KEY;     // ตั้งค่า API key
  auth.user.email = USER_EMAIL;  // ตั้งค่าอีเมลของบัญชี Firebase
  auth.user.password = USER_PASSWORD;    // ตั้งค่ารหัสผ่านของบัญชี Firebase
  config.database_url = DATABASE_URL;    // ตั้งค่า URL ของ Firebase Realtime Database
  Firebase.reconnectWiFi(true);     // ตั้งค่าให้ Firebase ทำการเชื่อมต่อ WiFi ใหม่เมื่อเกิดการตัดสัญญาณ
  fbdo.setResponseSize(4096);     // ตั้งค่าขนาดของข้อมูลที่ Firebase ส่งกลับมาให้เก็บไว้ใน Object
  config.token_status_callback = tokenStatusCallback; // ตั้งค่า callback function สำหรับการตรวจสอบสถานะของ Firebase token
  config.max_token_generation_retry = 5;      // ตั้งค่าจำนวนครั้งที่ Firebase จะทำการสร้าง token ใหม่หากเกิดข้อผิดพลาด
  Firebase.begin(&config, &auth);         // เริ่มต้นการทำงานของ Firebase
  Serial.println("Getting User UID");
  // ตรวจสอบสถานะของ Firebase token ว่ามีการสร้าง token แล้วหรือยัง
  while ((auth.token.uid) == "")
  {
    Serial.print('.');
    delay(1000);
  }
  databasePath = "GPS";            
  latPath = databasePath + "/lat";
  lonPath = databasePath + "/lon"; 
  namePath = databasePath + "/name";
  delay(5000);
  while (!sim808.attachGPS())
  {
    Serial.println("Open the GPS power failure");
  }
  Serial.println("Open the GPS power success");
}
void loop()
{
  switch (state)
  {
  case READ_LOCATION:
    if (sim808.getGPS() )
  {    latitute = String(sim808.GPSdata.lat);
    longtitute = String(sim808.GPSdata.lon);
    state = SEND_DATA;
  }else{
    Serial.println("GPS error");
    delay(1000);    
  }
    break;
  case SEND_DATA:
    sendDataToFirebase(latPath, latitute); // ส่งค่าอุณหภูมิ และ path ไปยังฟังก์ชัน sendDataToFirebase
    sendDataToFirebase(lonPath, longtitute);   // ส่งค่าความชื้น และ path ไปยังฟังก์ชัน sendDataToFirebase 
    sendDataToFirebase(namePath, "My location");   // ส่งค่าความชื้น และ path ไปยังฟังก์ชัน sendDataToFirebase
     delay(5000);
    state = READ_LOCATION;
    break;
  }
}
void sendDataToFirebase(String path, String value)
{
  // ถ้าส่งค่าไปยัง Firebase สำเร็จจะแสดงข้อความ "PASSED" และแสดง path และ type ของข้อมูลที่ Firebase ส่งกลับมา
  if (Firebase.RTDB.setString(&fbdo, path.c_str(), value))
  {
    Serial.print("Writing value: ");
    Serial.print(value);
    Serial.print(" on the following path: ");
    Serial.println(path);
    Serial.println("PASSED");
    Serial.println("PATH: " + fbdo.dataPath());
    Serial.println("TYPE: " + fbdo.dataType());
  }
  // ถ้าส่งค่าไปยัง Firebase ไม่สำเร็จจะแสดงข้อความ "FAILED" และแสดงเหตุผลที่เกิดข้อผิดพลาด
  else
  {
    Serial.println("FAILED");
    Serial.println("REASON: " + fbdo.errorReason());
  }
}

```
<p>
</details>

* จากโค้ดตัวอย่างที่ 7.2 รับค่า ละติจูด ลองจิจูด จากโมดูล SIM808 และส่งค่าไปที่ Firebase โดยมีการตั้ง databasePath มี latPath ใช้ส่งค่าละติจูด, lonPath ใช้ส่งค่าลองจิจูด และ namePath ส่งชื่อหมุดที่แสดงในแผนที่

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2020.58.19.png)

* จากรูป State Diagram การรับค่าลองติจูด ละติจูด 
เก็บข้อมูลที่ Firebase กำหนด state เริ่มต้นที่ READ_LOCATION โดยภายในสถานะนี้จะอ่านค่าลองติดจูด, ละติจูด หากอ่านไม่สำเร็จจะอยู่ state เดิม ถ้าอ่านค่าสำเร็จ state จะเปลี่ยนเป็น SEND_DAT จะทำการส่งค่าลองติจูดละติจูดแสดงบนหมุดเป็นเวลา 5 วินาที และค่า state จะเปลี่ยนไปที่ state READ_LOCATION จากนั้นจะทำงานซ้ำตามเงื่อนไขที่เรากำหนดไว้

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.01.37.png)

* จากรูปจะเห็น Flow มีการใช้ Node ของ Firebase ในการดึงข้อมูลจาก Realtime Database แล้วส่งไปยัง Node ชื่อ worldmap เพื่อแสดงแผนที่ โดยจะรับค่า Latitude , Longtitude  และชื่อหมุด เพื่อปักหมุดตามตำแหน่งปัจจุบัน โดยวิธีสร้าง Flow มีขั้นตอนดังนี้ 

1.	เลือก Node ชื่อ Firebase in มาวางบน Flow เพื่อดึงข้อมูลจาก Realtime Database 

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.01.59.png)

2. กดเข้าไปใน Node ตั้งค่า โดยกรอก Path ที่เราจะดึงข้อมูลจาก Realtime Database แล้ว กดปุ่มดินสอ เพื่อเพิ่ม Database 

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.02.17.png)

3. เมื่อเข้ามาแล้วให้ตั้งชื่อ Database ในส่วน Authentication Method เป็น Email จากนั้นในส่วน Credentials กรอก API key , URL Realtime Database , ที่อยู่ Email และ Password

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.02.47.png)

4.	 เลือก Node ชื่อ worldmap มาวางบน Flow เพื่อแสดงแผนที่
 
![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.02.59.png)

5.	เชื่อมต่อแต่ละ Node เข้าด้วยกันตามรูป

6.	กดปุ่ม Deploy มุมขวาบน เพื่อเริ่มการทำงานของ Flow
![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.03.07.png)

7.	ไปที่ Firebase เพื่อดูผลลัพธ์ และเปิดเว็ปดูแผนที่ดัง URL นี้ 
 http://localhost:1880/worldmap/ 
จากใน Firebase realtime database จะเห็นได้ว่ามีชุดข้อมูลที่ถูกเพิ่มเข้ามาอยู่ใน Path ชื่อ GPS ซึ่งจะมี lat , lon และ name เป็นต้น

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.03.16.png)

8.	จากใน Node red จะแสดงแผนที่ตำแหน่งปัจจุบันโดยเมื่อกดไปตรงหมุดจะแสดง ชื่อหมุด, Latitude และ Longtitude ปัจจุบัน

![image](image/ภาพถ่ายหน้าจอ%202566-10-10%20เวลา%2021.03.41.png)

#### คำถามท้ายบทที่ 7
1.	GPS คืออะไร
2.	Google map เป็นบริการเกี่ยวกับอะไร อธิบาย
3.	คุณลักษณะ Street View ใน Google Maps มีไว้เพื่ออะไร
4.	Google Maps ให้ข้อมูลเกี่ยวกับธุรกิจ ร้านอาหารและสถานที่น่าสนใจอื่นๆ ในบริเวณใกล้เคียงได้อย่างไร
5.	Firebase คืออะไร
6.	Firebase Cloud Firestore ดีกว่าระบบฐานข้อมูลแบบเดิมสำหรับหน้าเว็บและแอปพลิเคชันมือถืออย่างไร
7.	จะใช้ Firebase Authentication อย่างไรเพื่อให้มั่นใจความปลอดภัยในการเข้าถึงและควบคุมอุปกรณ์ IoT 
8.	JSON คือ
9.	JSON เก็บข้อมูลแบบไหน
10.	โมดูล node-red-contib-web-worldmap มีความสามารถอะไร อธิบายพอสังเขป 
 
#### ใบงาน 4 ตอน (แบบฝึก)
1.	กำหนดให้เปลี่ยนสีหมุดของ GPS
2.	กำหนดให้เปลี่ยน icon ของหมุด
3.	กำหนดให้เพิ่มรูปภาพในหมุด
4.	โจทย์ประยุกต์ วัดอุณหภูมิและความชื้น หากอุณหภูมิเกิน 25 ให้หมุดเป็นสีส้มและหากต่ำกว่า 25 ให้หมุดเป็นสีเขียว


ผลลัพธ์ใบงาน https://bit.ly/3PXi91N 