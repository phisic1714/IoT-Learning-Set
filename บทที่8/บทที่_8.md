# บทที่ 8 Gragph อุณหภูมิ ความชื้นผ่านเว็บไซต์

**บทนี้จะนำ Firebase กับ Node-RED มาประยุกต์ใช้ร่วมกัน**โดยจะให้ Node-RED ทำหน้าเว็บไซต์แสดง Dashboard ค่าอุณหภูมิ, กราฟอุณหภูมิและความชื้น โดยรับข้อมูลจากเซนเซอร์ SHTC3 จะสั่งเก็บไว้ใน Realtime Database ของ Firebase แล้วดึงข้อมูลออกมาโดย Library ของ Node-RED

####**node-red-dashboard**	
เป็นส่วนขยายที่เพิ่มความสามารถในการสร้างหน้าเว็ปโดยใช้ Node-RED โดยสามารถสร้าง Dashboard เพื่อดูค่าสถานะต่างๆจากอุปกรณ์ IoT โดยรับข้อมูลเข้ามาแสดง โดยรับค่าที่จะแสดงผ่าน Path คือ msg.payload และยังสามารถสร้างหน้าเว็ปเพื่อควบคุมอุปกรณ์ได้เพราะ สามารถกำหนดการส่งข้อมูลเป็นข้อความ ผ่านการกระทำบนหน้าเว็ป

#### **Node ต่าง ๆ สำหรับแสดงผลบน Node red dashboard**
•	**text** แสดงข้อความบนหน้าเว็ป สามารถกำหนดข้อความที่จะแสดง หรือรับค่าที่จะแสดง
•	**gauge** แสดงเกจวัดระดับบนหน้าเว็ป สามารถกำหนดระดับค่าที่จะแสดงโดยข้อมูลออกมาจะเป็นรูปแบบ เปอร์เซน และแสดงสีของค่าเตือนในเกณฑ์ ที่สูงๆ 
•	**chart** แสดงแผนภูมิเชิงเส้นโดยกำหนดแกน X เป็นช่วงเวลา ส่วนแกน Y เป็นค่าสูงสุด-ต่ำสุด 
•	**audio out** อ่านค่าจากข้อความเป็นเสียงออกมา สามารถกำหนดได้หลายภาษา Node นี้ต้องใช้ควบคู่กับ Node ชื่อ function
•	**notification** แสดงการแจ้งเตือนบนหน้าเว็ป สามารถกำหนดตำแหน่ง และการหน่วงเวลาในการแจ้งเตือน Node นี้ต้องใช้ควบคู่กับ Node ชื่อ function

**<details><summary>CLICK : <ins>ตัวอย่างที่ 8.1 สั่งปิดเปิด LED Built-in ผ่าน Node-RED Dashboard</ins></summary>**
<p>

```ruby
#include <Arduino.h>
#include "SHTC3.h"
#include <Firebase_ESP_Client.h> // Firebase library
#include <ESP8266WiFi.h>
#include "addons/TokenHelper.h" // Firebase token generation
#include "addons/RTDBHelper.h"  // Firebase Realtime Database helper
#define WIFI_SSID "xxxxx"
#define WIFI_PASSWORD "xxxxx"
#define API_KEY "xxxxx"         // API key ของ Firebase ที่ได้รับจากการสร้างโปรเจคใน Firebase Console
#define USER_EMAIL "xxxxx@xxxxx.xx"      // อีเมลของบัญชี Firebase ที่ใช้สำหรับเข้าสู่ระบบ
#define USER_PASSWORD "xxxxx"           // รหัสผ่านของบัญชี Firebase ที่ใช้สำหรับเข้าสู่ระบบ
#define DATABASE_URL "https://xxxxx.xxx/"      // URL ของ Firebase Realtime Database
void readTempHumid();                              // ฟังก์ชันสำหรับอ่านค่าอุณหภูมิและความชื้นจากเซ็นเซอร์
void sendDataToFirebase(String path, float value); // ฟังก์ชันสำหรับส่งค่าอุณหภูมิและความชื้นไปยัง Firebase
FirebaseData fbdo;          // Object สำหรับเก็บข้อมูลที่ได้จาก Firebase
FirebaseAuth auth;         // Object สำหรับเก็บข้อมูลการเข้าสู่ระบบ
FirebaseConfig config;       // Object สำหรับเก็บข้อมูลการเชื่อมต่อ Firebase
String uid;                        // ตัวแปรสำหรับเก็บค่า User ID
String databasePath;            // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database
String tempPath;                    // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database สำหรับอุณหภูมิ
String humPath;                    // ตัวแปรสำหรับเก็บค่า path ของ Firebase Realtime Database สำหรับความชื้น
SHTC3 shtc3(Wire);                                 
float temperature;                                
float humidity;                                   
const int READ_TEMP_HUMID = 0;
const int SEND_TEMP_HUMID = 1;
int state;
void initWiFi()
{
    WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
    Serial.print("Connecting to WiFi ..");
    while (WiFi.status() != WL_CONNECTED)
    {
        Serial.print('.');
        delay(1000);
    }
    Serial.println(WiFi.localIP());
    Serial.println();
}
void setup()
{
    state = READ_TEMP_HUMID;
    Serial.begin(115200);
    Wire.begin();
    initWiFi();
    config.api_key = API_KEY; // ตั้งค่า API key
    auth.user.email = USER_EMAIL; // ตั้งค่าอีเมลของบัญชี Firebase
    auth.user.password = USER_PASSWORD; // ตั้งค่ารหัสผ่านของบัญชี Firebase
    config.database_url = DATABASE_URL; // ตั้งค่า URL ของ Firebase Realtime Database
    Firebase.reconnectWiFi(true); // ตั้งค่าให้ Firebase ทำการเชื่อมต่อ WiFi ใหม่เมื่อเกิดการตัดสัญญาณ
    fbdo.setResponseSize(4096); // ตั้งค่าขนาดของข้อมูลที่ Firebase ส่งกลับมาให้เก็บไว้ใน Object
    config.token_status_callback = tokenStatusCallback; // ตั้งค่า callback function สำหรับการตรวจสอบสถานะของ Firebase token
    config.max_token_generation_retry = 5; // ตั้งค่าจำนวนครั้งที่ Firebase จะทำการสร้าง token ใหม่หากเกิดข้อผิดพลาด
    Firebase.begin(&config, &auth); // เริ่มต้นการทำงานของ Firebase
    Serial.println("Getting User UID");
    // ตรวจสอบสถานะของ Firebase token ว่ามีการสร้าง token แล้วหรือยัง
    while ((auth.token.uid) == "")
    {
        Serial.print('.');
        delay(1000);
    }
    databasePath = "Sensor"; // ตั้งค่า path ของ Firebase Realtime Database
    tempPath = databasePath + "Temperature"; // ตั้งค่า path ของ Firebase Realtime Database สำหรับอุณหภูมิ
    humPath = databasePath + "Humidity";     // ตั้งค่า path ของ Firebase Realtime Database สำหรับความชื้น
}
void loop()
{
    switch (state)
    {
    case READ_TEMP_HUMID:
        readTempHumid();
        state = SEND_TEMP_HUMID;
        break;
    case SEND_TEMP_HUMID:
        sendDataToFirebase(tempPath, temperature); // ส่งค่าอุณหภูมิ และ path ไปยังฟังก์ชัน sendDataToFirebase
        sendDataToFirebase(humPath, humidity); // ส่งค่าความชื้น และ path ไปยังฟังก์ชัน sendDataToFirebase
        state = READ_TEMP_HUMID;
        break;
    }
}
void readTempHumid()
{
    shtc3.begin(true);
    shtc3.sample();
    // ถ้า Firebase พร้อมทำงาน จะเก็บค่าอุณหภูมิและความชื้น
    if (Firebase.ready())
    {
        delay(2000);
        temperature = shtc3.readTempC();
        humidity = shtc3.readHumidity();
    }
}
void sendDataToFirebase(String path, float value)
{
    // ถ้าส่งค่าไปยัง Firebase สำเร็จจะแสดงข้อความ "PASSED" และแสดง path และ type ของข้อมูลที่ Firebase ส่งกลับมา
    if (Firebase.RTDB.setFloat(&fbdo, path.c_str(), value))
    {
        Serial.print("Writing value: ");
        Serial.print(value);
        Serial.print(" on the following path: ");
        Serial.println(path);
        Serial.println("PASSED");
        Serial.println("PATH: " + fbdo.dataPath());
        Serial.println("TYPE: " + fbdo.dataType());
    }
    // ถ้าส่งค่าไปยัง Firebase ไม่สำเร็จจะแสดงข้อความ "FAILED" และแสดงเหตุผลที่เกิดข้อผิดพลาด
    else
    {
        Serial.println("FAILED");
        Serial.println("REASON: " + fbdo.errorReason());
    }
}
```
<p>
</details>

* **จากตัวอย่างโค้ด 8.1** เชื่อมต่ออินเทอร์เน็ตในรูปแบบไวฟาย, เชื่อมต่อ Firebase โดยมีการกำหนด Path คือ /Sensor/Temperature และ /Sensor/Humidity โดยการทำงานจะเข้าสถานะเริ่มต้นคือ READ_DATA เพื่ออ่านข้อมูล Sensor แล้วข้อมูลจาก Float เป็น String และสถานะต่อมาคือ SEND_DATA จะส่งข้อมูลที่ได้จากกา

* State Diagram อ่านค่าอุณหภูมิและส่งค่าอุณหภูมิ แสดงบนเว็บไซต์ state เริ่มต้นที่ READ_TEMP_HUMID โดยภายในสถานะนี้จะอ่านค่าอุณหภูมิและความชื้น state จะเปลี่ยนเป็น SEND_TEMP_HUMID ส่งค่าอุณหภูมิและความชื้นไปที่ Firebase และ state จะเปลี่ยนไปเป็น READ_TEMP_HUMID จากนั้นจะทำงานซ้ำ

จากรูปจะเห็น Flow มีการใช้ Node ของ Firebase 2 ตัว ในการดึงข้อมูลอุณหภูมิและความชื้น จาก Realtime Database มาส่งไปยัง Node ของ Dashboard ชื่อ chart ที่จะทำหน้าที่แสดงกราฟเชิงเส้น โดยวิธีสร้าง Flow มีขั้นตอนดังนี้

1.	เลือก Node ชื่อ Firebase in ของ Firebase สำหรับรับค่าอุณหภูมิจาก Realtime Database 

2.	กดเข้าไปใน Node ตั้งค่าโดยเลือก Database ที่ตั้งไว้ แล้ว กรอก Path ที่เก็บค่าอุณหภูมิในที่นี้คือ “/Sensor/Temperature” และกด Done

3.	เลือก Node ชื่อ Chart ของ dashboard สำหรับแสดงกราฟอุณหภูมิ

4.	กดเข้าไปใน Node โดยตั้งชื่อ Label ในที่นี้กำหนดเป็น “Temperature Graph” แล้วเปลี่ยนหน่วยเวลาของแกน X เป็น minutes และกดปุ่มรูปดินสอ เพื่อเพิ่ม Group ของ Dashboard

5.	เมื่อเข้ามาแล้วให้ตั้งชื่อ Group ในที่นี้คือ “Temperature Dashboard” แล้วกดปุ่ม Add และจะออกมาหน้าในข้อที่ 4 ให้กดปุ่ม Done

6.	เลือก Node ชื่อ Chart ของ dashboard สำหรับแสดงกราฟความชื้น

7.	กดเข้าไปใน Node ตั้งค่าดังรูปที่ โดยตั้งชื่อ Label ในที่นี้คือ “Humidity Graph” แล้วเปลี่ยนหน่วยเวลาของแกน X เป็น minutes และกดปุ่มรูปดินสอ เพื่อเพิ่ม Group ของ Dashboard 
8.	เมื่อเข้ามาแล้วให้ตั้งชื่อ Group ในที่นี้คือ “Humidity Dashboard” แล้วกดปุ่ม Add และจะออกมาหน้าในข้อที่ 9 ให้กดปุ่ม Done กด
9.	เชื่อมต่อแต่ละ Node เข้าด้วยกันตามรูปที่  8.3
10.	กดปุ่ม Deploy มุมขวาบน เพื่อเริ่มการทำงานของ Flow
11.	ไปที่ Firebase เพื่อดูผลลัพธ์ และเปิดเว็ปดู Dashboard URL : http://localhost:1880/ui/
จากใน Firebase realtime database จะเห็นได้ว่ามีชุดข้อมูลที่ถูกเพิ่มเข้ามาอยู่ใน Path ชื่อ Sensor ซึ่งจะมี Humidity และTemperature เป็นต้น

จากในเว็ป Dashboard จะแสดงกราฟ อุณหภูมิ และความชื้น โดยมีชื่อ Group เป็นสีฟ้า และ ชื่อ Label ส่วนตัวกราฟจะเปลี่ยนแปลงเป็นระดับนาที ตามที่เราตั้งไว้ใน Node

####คำถามท้ายบทที่ 8
1.	node-red-dashboard คืออะไร
2.	หากต้องการเข้าหน้า Dashboard ของ NodeRED โดย localhost คือhttps://localhost:1880/ สามารถเข้าได้อย่างไร
3.	node-red-dashboard การดึงค่ามาแสดงบน Dashboard จะต้องดึงผ่าน Path อะไร
4.	การกำหนด Node ชื่อ gauge จะแสดงโดยข้อมูลออกมาจะเป็นรูปแบบอะไร
5.	สามารถกำหนดให้แสดงข้อมูลในรูปแบบกราฟได้อย่างไร
6.	อธิบายการกำหนดรับค่าอุณหภูมิจาก Realtime Database บน Node-RED ทำอย่างไร อธิบาย
7.	การใช้งาน Node ชื่อ notification จะต้องใช้ Node ใดควบคู่กัน
8.	การกำหนด Node ชื่อ chart ข้อมูล แกน X และ แกน Y คืออะไร
9.	หากต้องการให้มีเสียงอ่านข้อความควรใช้ Node ใด
10.	หากต้องการให้แสดงค่าความชื้นเป็นตัวเลขอย่างเดียวบนหน้าเว็ปควรใช้ Node ใด

####ใบงาน 4 ตอน (แบบฝึก)
1)	จากตัวอย่าง 8.1 ให้เพิ่ม Text node แสดงค่าอุณหภูมิ และความชื้น
2)	จากข้อที่ 1 ให้เพิ่ม Gauge node โดยอุณหภูมิ Gauge จะอยู่ในช่วง 0-50 และ ความชื้น Gauge จะอยู่ในช่วง 0-100
3)	จากข้อ2 ให้เพิ่ม audio out node อ่านออกเสียงค่าอุณหภูมิ
4)	กำหนดให้ทำระบบแจ้งเตือนเมื่ออุณหภูมิสูงเกิน 30 องศา โดยใช้ function node เป็นตัวกำหนดเงื่อนไขการทำงาน
